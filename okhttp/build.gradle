apply plugin: "com.android.library"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven"
apply plugin: "maven-publish"

android
{
  compileSdkVersion 27

  defaultConfig
  {
    minSdkVersion 14
    targetSdkVersion 27

    versionCode 14
    versionName "1.2.0"
  }

  buildTypes
  {
    release
    {
      minifyEnabled false
      zipAlignEnabled true
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
  }
}

dependencies
{
  //Smart&Soft
  api ("com.smartnsoft:droid4me:2.8.0")
  api ("com.smartnsoft:droid4me.ext:2.7.0")

  //Third party libraries
  api ("com.squareup.okhttp3:okhttp:3.10.0")
}

task sourcesJar(type: Jar) {
  classifier = "sources"
  from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
  title = "okhttp API"
  description "Javadoc for the okhttp web service caller implementation"
  source = android.sourceSets.main.java.srcDirs

  options.charSet = "UTF-8"
  options.overview = "src/main/javadoc/overview.html"
  options.links = ["http://docs.oracle.com/javase/7/docs/api/", "http://d.android.com/reference/"]

  classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
  failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = "javadoc"
  from javadoc.destinationDir
  // options.encoding = 'UTF-8'
}

artifacts
{
  archives javadocJar
  archives sourcesJar
}

def pomConfig = {
  licenses
  {
    license
    {
      name "MIT License (MIT)"
      url "https://opensource.org/licenses/MIT"
      distribution "repo"
    }
  }
}

publishing
{
  publications
  {
    ReleasePublication(MavenPublication)
    {
      artifact sourcesJar
      artifact javadocJar
      groupId "com.smartnsoft"
      artifactId "okhttpwebservicecaller"
      version android.defaultConfig.versionName

      pom.withXml
      {
        def root = asNode()
        root.appendNode("description", "extension of the droid4me framework which contains the okhttp web service caller implementation")
        root.appendNode("name", "okhttpwebservicecaller")
        root.appendNode("url", "https://github.com/smartnsoft/droid4me.ws")
        root.children().last() + pomConfig
      }
    }
  }
}

//https://gist.github.com/bugs84/b7887fb5d7f9f2d484b8
publishing.publications.all
{
  pom.withXml
  {
    asNode().dependencies."*".findAll()
    {
      it.scope.text() == "runtime" && project.configurations.compile.allDependencies.find { dep ->
        dep.name == it.artifactId.text()
      }
    }.each
    {
      it.scope*.value = "compile"
    }
  }
}

bintray
{
  user = findProperty("bintrayUsername") ?: ""
  key = findProperty("bintrayKey") ?: ""

  publications = ["ReleasePublication"]

  pkg
  {
    repo = "maven"
    name = "okhttpwebservicecaller"
    userOrg = "smartnsoft"
    licenses = ["MIT"]
    vcsUrl = "https://github.com/smartnsoft/droid4me.ws.git"
    publish = true

    version
    {
      name = android.defaultConfig.versionName
      desc = "extension of the droid4me framework which contains the okhttp web service caller implementation"
      released  = new Date()
      vcsTag = "okhttpwebservicecaller-" + android.defaultConfig.versionName
    }
  }
}

uploadArchives
{
  repositories
  {
    mavenDeployer
    {
      repository(url: "https://api.bintray.com/maven/smartnsoft/maven/okhttpwebservicecaller/;publish=1")
      {
        authentication(userName: findProperty("bintrayUsername") ?: "", password: findProperty("bintrayKey") ?: "")
      }

      snapshotRepository(url: findProperty("nexusSnapshotUrl") ?: "")
      {
        authentication(userName: findProperty("nexusUsername") ?: "", password: findProperty("nexusPassword") ?: "")
      }

      pom.project
      {
        name "okhttpwebservicecaller"
        groupId "com.smartnsoft"
        artifactId "okhttpwebservicecaller"
        version android.defaultConfig.versionName
        packaging "aar"
      }
    }
  }
}